cmake_minimum_required(VERSION 3.6)
project(opengl-sine-wave VERSION 0.1 LANGUAGES CXX)
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

if(MSVC)
  add_compile_options(/utf-8)
endif(MSVC)

# Find QT Library
set(QT_MAJOR_VERSION 5 CACHE STRING "Major version of QT")
find_package(Qt${QT_MAJOR_VERSION} REQUIRED COMPONENTS
  Widgets
)
set(CMAKE_AUTOUIC ON)
set(CMAKE_AUTOMOC ON)
set(CMAKE_AUTORCC ON)

# Fetch GLM library
include(FetchContent)
FetchContent_Declare(GLM
  URL "https://github.com/g-truc/glm/releases/download/0.9.9.8/glm-0.9.9.8.zip"
)
FetchContent_MakeAvailable(GLM)

# Executables
add_executable(sine-wave
  src/gl.cpp # Generated by glad
  src/main.cpp
  src/WaveWidget.cpp src/WaveWidget.h
  src/shader/wave.vert src/shader/wave.frag
)
# Set include directories
target_include_directories(sine-wave PRIVATE include)
# Make it a GUI application
set_target_properties(sine-wave PROPERTIES
  WIN32_EXECUTABLE True
  MACOSX_BUNDLE True
)
# Link libraries
target_link_libraries(sine-wave PRIVATE
  Qt${QT_MAJOR_VERSION}::Widgets
  glm::glm
)

add_custom_target(copy-shader
  COMMAND ${CMAKE_COMMAND} -E copy_directory "src/shader" ${CMAKE_CURRENT_BINARY_DIR}/shader
  WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
  VERBATIM
)
add_dependencies(sine-wave copy-shader)
